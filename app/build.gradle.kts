plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

// Version aus der CI: Build-Nummer (GitHub-Lauf) und Commit; lokal ohne CI = 1 / «lokal».
val runNumber = System.getenv("GITHUB_RUN_NUMBER")?.toIntOrNull() ?: 1
val gitSha = System.getenv("GITHUB_SHA")?.take(7) ?: "lokal"

android {
    namespace = "ch.sonnhalde.fotobox"
    compileSdk = 34

    defaultConfig {
        applicationId = "ch.sonnhalde.fotobox"
        // API 29+: Dateien koennen ohne Speicher-Berechtigung per MediaStore in "Downloads" gespeichert werden.
        minSdk = 29
        targetSdk = 34
        versionCode = runNumber
        versionName = "0.1.$runNumber"
        buildConfigField("String", "GIT_SHA", "\"$gitSha\"")
    }

    // Fester Debug-Schluessel (nicht geheim): jeder CI-Build ist gleich signiert, daher laesst sich ein
    // neuer Build direkt ueber die installierte App installieren, ohne vorher zu deinstallieren.
    signingConfigs {
        getByName("debug") {
            storeFile = file("debug.keystore")
            storePassword = "android"
            keyAlias = "androiddebugkey"
            keyPassword = "android"
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = false
        }
    }
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        compose = true
        buildConfig = true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.5.14"
    }
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.06.00")
    implementation(composeBom)
    implementation("androidx.activity:activity-compose:1.9.1")
    implementation("androidx.lifecycle:lifecycle-viewmodel-compose:2.8.4")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-tooling-preview")
    debugImplementation("androidx.compose.ui:ui-tooling")

    val camerax = "1.3.4"
    implementation("androidx.camera:camera-core:$camerax")
    implementation("androidx.camera:camera-camera2:$camerax")
    implementation("androidx.camera:camera-lifecycle:$camerax")
    implementation("androidx.camera:camera-view:$camerax")
    implementation("androidx.print:print:1.0.0")

    implementation("com.squareup.okhttp3:okhttp:4.12.0")
    implementation("com.google.zxing:core:3.5.3")
}
